// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter() // add by guodanyang
        maven { url "http://mirrors.ibiblio.org/maven2"}
        google()
        // for gradle-task-tree，执行：gradle **Task taskTree --no-repeat，即可查看task依赖关系（参考：https://www.jianshu.com/p/6599de4cdcd1）
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        // for gradle-task-tree，1.2.2版本下载失败，使用1.3版本
//        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.2.2"
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3"
        // for gradle-visteg，
        classpath 'cz.malohlava:visteg:1.0.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

// for gradle-task-tree
apply plugin: "com.dorongold.task-tree"

apply plugin: 'cz.malohlava.visteg'
// for gradle-visteg
visteg {
    enabled        = true
    colouredNodes  = true
    colouredEdges  = true
    destination    = 'build-outputs/reports/visteg.dot'
    exporter       = 'dot'
    colorscheme    = 'spectral11'
    nodeShape      = 'box'
    startNodeShape = 'hexagon'
    endNodeShape   = 'doubleoctagon'
}

ext{

    //这是工程根目录
    ctripRoot = project(":").projectDir

    // local.properties 来自于AS自动创建
    // 可以保存所有本地工程配置
    // 不允许上传git库
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    // 从系统环境变量中或者local.properties配置文件中读取SDK位置
    if(System.getenv("ANDROID_HOME")!=null){
        sdkDir = System.getenv("ANDROID_HOME")
    }
    else{ // 在local.properties中定义
        sdkDir = properties.getProperty('sdk.dir')
    }

    //Debug代码：开发人员可以手动改为false，这样工程就是标准Android工程，可供开发调试。
    //改为true，请使用 gradle assembleRelease bundleRelease repackAll 命令打出多apk的release包。
    solidMode = true // 是否使用插件化
//    solidMode = false // Debug by guodanyang
    //可以在local.properties里修改这个值。添加一行 solidMode=false 即可。
    solidModeConfigValue = properties.getProperty('solidMode')
    if('true'.equalsIgnoreCase(solidModeConfigValue)) {
        solidMode = true
    }
    else if ('false'.equalsIgnoreCase(solidModeConfigValue)) {
        solidMode = false
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}
